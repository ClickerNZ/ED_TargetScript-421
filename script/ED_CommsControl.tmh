
//----------------------------------------------------------------------//
//	CMDR Clicker's Elite Dangerous Thrustmaster Warthog + TFRP Script	//
//----------------------------------------------------------------------//

// This file: ED_CommsControl.tmh (v4.2.0) ... supports Elite Dangerous v3.7 (Fleet Carriers)

// Refer Change Log.txt in same folder

// LEGEND: 
// ========================================================
// fnName()					refer to ED_Functions file
// initName()				refer to ED_Functions file
// fnsName()				refer to ED_StateTracker file
// tgName()					refer to ED_Toggles file
// mName					refer to ED_Macros file
// MapKey statements		refer to ED_MapKeyAssignment file
// Global Defines			refer to ED_Defines file
// Global Variables			refer to ED_GlobalVars file
// User Config Settings		refer to ED_UserSettings file
// ========================================================

// For the purposes of this exercise I'm going to use the following;
//
// Channel 0:	All Voice Channels MUTED
// Channel 1:	Voice Attack
// Channel 2:	In game Voice Comms (Game = Elite Dangerous)
// Channel 3:	Discord
// Channel 4:	All Voice Channels OPEN

// STATUS:		TESTING

// Index:	Line		FUNCTION
//			Line 118:	initCommsChannels()
//			Line 151:	fnChannelSelect()
//			Line 182:	fnMicControl()
//			Line 290:	tgPTTMode()
//			Line 301:	fnCommStatus()
		
// Global Vars
// =================================================================================================== 
	define MuteChannel1			L_ALT+USB[0x3B]			// LALT+F2		(Map this to VoiceAttack)
	define MuteChannel2			L_ALT+USB[0x3C]			// LALT+F3		(Map this to E:D in game Voice Channel
//	define MuteChannel3			L_ALT+USB[0x55]			// LALT+NUMPAD * This causes TARGET to play windows 'brring' wav??
	define MuteChannel3			L_ALT+USB[0x3E]			// LALT+F5		(Map this to Discord/Twitch/OBS/Youtube)

// General

	define CHNLUP				1			// Channel UP
	define CHNLDN				0			// Channel DOWN 

// Radio Modes

	int PTTMode				=	0;			// HOLD or TOGGLE:	0 = Press to OPEN, Release to Close, 1 = Press to OPEN, Press to CLOSED
	int Channel				=	0;			// CHANNEL:			0 = Off, 1 = Internal, 2 = External-1, 3 = External-2, 4 = All Open

// Channel Selection						// Default: All channels OPEN

	int CH1 				=	1;			// 0 = muted, 1 = open
	int CH2					=	1;			// 0 = muted, 1 = open
	int	CH3 				=	1;			// 0 = muted, 1 = open
		
// Microphone status

	int mike				= 	0;			// 0 = muted, 1 = open

// Initialisation status

	int CommsInitialised	=	0;
	
		
// End Global Variables	

// =================================================================================================== 

//	FUNCTION:	Initialise Radio - Mute Everything

	int initCommsChannels()	{
	
		// Set Channel to 0
		Channel = 0;
		
		// Mute all channels

		ActKey(CHAIN(
				LOCK,
				PULSE+KEYON+MuteChannel1, D(),
				PULSE+KEYON+MuteChannel2, D(),
				PULSE+KEYON+MuteChannel3, D(),
				LOCK			
			)
		);

		// No Channels selected...Set Status to MUTE
		CH1 = 0;
		CH2 = 0;
		CH3 = 0;
		
		// Set microphone to mute
		mike = 0;
		
		// Set PTT Mode to HOLD
		PTTMode = 0;
		
		CommsInitialised = 1;
		
	}

//	FUNCTION:	Channel selector

	int fnChannelSelect(int x) {
		
		if (x) Channel	= Channel + 1;
		else Channel 	= Channel - 1;
		
		if (Channel < 0) Channel = 4;
		if (Channel > 4) Channel = 0;
		
		if (Channel == 0) {		
			VoiceTxt = "All Channels muted";
		}
		if (Channel == 1) {				// Voice Attack 
			VoiceTxt = "Channel 1";
		}
		if (Channel == 2) {				// Internal 
			VoiceTxt = "Channel 2";
		}
		if (Channel == 3) {				//  External
			VoiceTxt = "Channel 3";
		}
		if (Channel == 4) {
			VoiceTxt = "All Channels Open";
		}
		
		printf("Comm Select:	%s\x0a", VoiceTxt);
		fnTextToSpeech(VoiceTxt);
	
	}
	
// FUNCTION: Controls the communications functions. Uses &Throttle[MSP] as PTT
	
	int fnMicControl(int PTT) {
		if (PTTMode) {						// Push and Hold To Talk
			if (PTT) {
				mike = 1;
				printf("Mike = %d\x0a", mike);
			}
			else {							// Release to Mute 
				mike = 0;
				printf("Mike = %d\x0a", mike);
			}
		}
		else {								// Toggle Talk/Mute 
			if (PTT) {
				mike = !mike;
				printf("Mike = %d\x0a", mike);
			}
		}
		if (mike == 1) {
			if (Channel == 0) {
				if (CH1) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
					CH1 = 0;
				}
				if (CH2) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
					CH2 = 0;
				}
				if (CH3) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
					CH3 = 0;
				}
			}
			if (Channel == 1) {
				if (!CH1) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
					CH1 = 1;
				}
				if (CH2) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
					CH2 = 0;
				}
				if (CH3) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
					CH3 = 0;
				}
			}
			if (Channel == 2) {
				if (CH1) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
					CH1 = 0;
				}
				if (!CH2) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
					CH2 = 1;
				}
				if (CH3) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
					CH3 = 0;
				}
			}
			if (Channel == 3) {
				if (CH1) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
					CH1 = 0;
				}
				if (CH2) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
					CH2 = 0;
				}
				if (!CH3) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
					CH3 = 1;
				}
			}
			if (Channel == 4) {
				if (!CH1) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
					CH1 = 1;
				}
				if (!CH2) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
					CH2 = 1;
				}
				if (!CH3) {
					ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
					CH3 = 1;
				}
			}			
		}
		else {
			if (CH1) {
				ActKey(CHAIN(PULSE+KEYON+MuteChannel1, D()));
				CH1 = 0;
			}
			if (CH2) {
				ActKey(CHAIN(PULSE+KEYON+MuteChannel2, D()));
				CH2 = 0;
			}
			if (CH3) {
				ActKey(CHAIN(PULSE+KEYON+MuteChannel3, D()));
				CH3 = 0;
			}			
		}
	}
	

//	TOGGLE:	PTT Mode = HOLD or TOGGLE ON/OFF 
	
	int tgPTTMode() {
		int VoiceTxt;
		
		PTTMode = !PTTMode;
		if (PTTMode) 	VoiceTxt = "PTT Mode: HOLD";
		else			VoiceTxt = "PTT Mode: Toggle";

//		printf("%s\x0a", VoiceTxt);
//		fnTextToSpeech(VoiceTxt);
		fnVPOutput(VoiceTxt);
	}
	
// FUNCTION: Report Comms Status

	int fnCommStatus() {
		char s;
		Dim(&s, 64);
		s[0] = 0;
		
		int mic;
		if (mike)	mic = "open";
		else		mic = "muted";
		
		if (PTTMode)	VoiceTxt = "hold";
		else			VoiceTxt = "toggle";
		
		sprintf(&s, "PTT Mode %s, Channel %d, mike %s", VoiceTxt, Channel, mic);
//		fnTextToSpeech(&s);
		fnVPOutput(&s);
	}
		
// END
	